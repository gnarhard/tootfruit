import org.jetbrains.kotlin.gradle.tasks.KotlinCompile

buildscript {
    ext.kotlin_version = '2.2.21'
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:8.13.0'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

rootProject.buildDir = '../build'
subprojects {
    project.buildDir = "${rootProject.buildDir}/${project.name}"
}
subprojects {
    project.evaluationDependsOn(':app')
}

subprojects {
    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ['-Xlint:-options']
    }
}

subprojects {
    tasks.withType(KotlinCompile).configureEach {
        kotlinOptions {
            freeCompilerArgs += ['-nowarn']
        }
    }
}

subprojects { subproject ->
    if (subproject.name == "flutter_soloud") {
        subproject.afterEvaluate {
            if (subproject.hasProperty("android")) {
                subproject.android {
                    defaultConfig {
                        externalNativeBuild {
                            cmake {
                                // Suppress noisy upstream C/C++ warnings from third-party native sources.
                                arguments "-Wno-dev", "-DCMAKE_C_FLAGS=-w", "-DCMAKE_CXX_FLAGS=-w"
                            }
                        }
                    }
                }
            }
        }
    }
}

tasks.register("clean", Delete) {
    delete rootProject.buildDir
}
